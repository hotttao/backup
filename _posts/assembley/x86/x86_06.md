---
title: 6. 中断
date: 2021-07-06
categories:
    - Go
tags:
	- x86 汇编
---

中断
<!-- more -->

## 1. 内容概述
今天我们来介绍计算中另一个非常重要的机制。不知道大家思考过这个问题没有，在单核处理器上，程序是如何实现调度(切换)的。当一个程序执行时，它是不会知道还有别的程序正眼巴巴地等着执行。在这种情况下，中断（Interrupt）这种工作机制就应运而生了。接下来我们就来介绍中断相关的内容，包括:
1. 了解中断的原理和分类
      - 外部硬件中断
      - 内部中断
      - 软中断
2. 时钟的实现和时钟中断


## 2. 外部硬件中断
外部硬件中断，就是从处理器外面来的中断信号。如下图所示，外部硬件中断是通过两个信号线引入处理器内部的。这两根线的名字就叫NMI 和INTR。

![外部硬件中断物理结构](/images/assembly/intr.png)

芯片设计者通过两个引脚来明确区分不同性质的中断:
1. 非屏蔽中断（Non Maskable Interrupt，NMI）
2. 可屏蔽中断，通过INTR 引脚进入处理器内部的

中断信号的来源，或者说，产生中断的设备，称为中断源，每种类型的中断都被统一编号，这称为中断类型号、中断向量或者中断号。但是，由于不可屏蔽中断的特殊性——几乎所有触发NMI 的事件对处理器来说都是致命的，甚至是不可纠正的，在实模式下，NMI 被赋予了统一的中断号2，不再进行细分。



### 2.1 中断控制器
处理器每次只能处理一个中断，而中断是很有可能并发发生的，所以处理器需要一个代理，来接受外部设备发出的中断信号，并对同时到来的中断进行排队。

在个人计算机中，用得最多的中断代理就是8259 芯片，它就是通常所说的中断控制器，其结构如下图所示:

![中断控制器](/images/assembly/8259.png)

Intel 处理器允许256 个中断，中断号的范围是0～255，8259 负责提供其中的15 个，但中断号并不固定。中断控制器芯片有自己的端口号，可以像访问其他外部设备一样用in 和out 指令来改变它的状态，包括各引脚的中断号。因此它又叫可编程中断控制器（Programmable Interrupt Controller，PIC）。

当有多个中断同时发生时，8259 芯片会记住它们，并按一定的策略决定先为谁服务。总体上来说，中断的优先级和引脚是相关的，主片的IR0 引脚优先级最高，IR7 引脚最低，从片也是如此。

#### 中断屏蔽
在8259 芯片内部，有中断屏蔽寄存器（Interrupt Mask Register，IMR），这是个8 位寄存器，对应着该芯片的8 个中断输入引脚，对应的位是0 还是1，决定了从该引脚来的中断信号是否能够通过8259 送往处理器（0 表示允许，1 表示阻断，这可能出乎你的意料）。8259 芯片是可编程的，主片的端口号是0x20 和0x21，从片的端口号是0xa0 和0xa1，可以通过这些端口访问8259 芯片，设置它的工作方式，包括IMR 的内容。

中断能否被处理，除了要看8259 芯片的脸色外，最终的决定权在处理器手中。在处理器内部，标志寄存器有一个标志位IF，这就是中断标志（Interrupt Flag）。当IF 为0 时，所有从处理器INTR 引脚来的中断信号都被忽略掉；当其为1 时，处理器可以接受和响应中断。

IF 标志位可以通过两条指令cli 和sti 来改变。cli（CLear Interrupt flag）用于清除IF 标志位，sti（SeT Interrupt flag）用于置位IF 标志。

### 2.2 中断向量表
所谓中断处理，归根结底就是处理器要执行一段与该中断有关的程序（指令）。在实模式下，处理器要求将它们的入口点集中存放到内存中从物理地址0x00000 开始，到0x003ff 结束，共1KB 的空间内。这就是所谓的中断向量表（Interrupt Vector Table，IVT）。

如下图所示，每个中断在中断向量表中占2 个字，分别是中断处理程序的偏移地址和段地址。

![中断向量表](/images/assembly/ivt.png)

当中断发生时，处理器在执行完当前的指令后首先会响应中断，告诉8259 芯片准备着手处理该中断。接着，它还会要求8259 芯片把中断号送过来。处理器拿着这个中断号，要顺序做以下几件事:
1. 保护断点的现场。首先要将标志寄存器FLAGS 压栈，然后清除它的IF 位和TF 位。
2. 接着，再将当前的代码段寄存器CS 和指令指针寄存器IP 压栈。
3. 执行中断处理程序。中断号乘以4 就是该中断入口点在中断向量表中的偏移地址
4. 注意，由于IF 标志被清除，在中断处理过程中，处理器将不再响应硬件中断。如果希望更高优先级的中断嵌套，可以在编写中断处理程序时，适时用sti 指令开放中断
5. 返回到断点接着执行。所有中断处理程序的最后一条指令必须是中断返回指令iret。这将导致处理器依次从栈中弹出（恢复）IP、CS 和FLAGS 的原始内容，于是转到主程序接着执行。

和可屏蔽中断不同，NMI 发生时，处理器不会从外部获得中断号，它自动生成中断号码2，其他处理过程和可屏蔽中断相同。

中断随时可能发生，中断向量表的建立和初始化工作是由BIOS 在计算机启动时负责完成的。BIOS 为每个中断号填写入口地址，因为它不知道多数中断处理程序的位置，所以，一律将它们指向一个相同的入口地址，在那里，只有一条指令：iret。也就是说，当这些中断发生时，只做一件事，那就是立即返回。当计算机启动后，操作系统和用户程序再根据自己的需要，来修改某些中断的入口地址，使它指向自己的代码。

## 3. 时钟
### 3.1 CMOS
计算机能显示时间是因为在外围设备控制器芯片ICH 内部，集成了实时时钟电路（Real Time Clock，RTC）和两小块由互补金属氧化物（CMOS）材料组成的静态存储器（CMOS RAM）。实时时钟电路负责计时，而日期和时间的数值就存储在在CMOS中。

CMOS RAM 中的，通常有128 字节，而日期和时间信息只占了一小部分容量，其他空间则用于保存整机的配置信息，比如各种硬件的类型和工作参数、开机密码和辅助存储设备的启动顺序等。这些参数的修改通常在BIOS SETUP 开机程序中进行。

### 3.2 RTC
RTC 芯片由一个振荡频率为32.768kHz 的石英晶体振荡器（晶振）驱动，经分频后，用于对CMOS RAM 进行每秒一次的时间刷新。

如下图所示:
1. 常规的日期和时间信息占据了CMOS RAM 开始部分的10 字节，有年、月、日和时、分、秒
2. 报警的时、分、秒用于产生到时间报警中断，如果它们的内容为0xC0～0xFF，则表示不使用报警功能。
3. 单元0x0A～0x0D 不是普通的存储单元，而被定义成4 个寄存器的索引号，也是通过0x70 和0x71 这两个端口访问的。这4 个寄存器用于设置实时时钟电路的参数和工作状态。
4. 寄存器A 和B 用于对RTC 的功能进行整体性的设置
5. 寄存器C 和D 是标志寄存器，这些标志反映了RTC 的工作状态，寄存器C 是只读的，寄存器D 则可读可写。特别是寄存器C，因为RTC 可以产生中断，当中断产生时，可以通过该寄存器来识别中断的原因，比如，是周期性的中断，还是闹钟中断。

![cmos存储时间的位](/images/assembly/cmos_time.png)

CMOS RAM 的访问，需要通过两个端口来进行。0x70 或者0x74 是索引端口，用来指定CMOS RAM 内的单元；0x71 或者0x75 是数据端口，用来读写相应单元里的内容。

**端口0x70 的最高位（bit 7）是控制NMI 中断的开关。当它为0 时，允许NMI 中断到达处理器，为1 时，则阻断所有的NMI 信号**，其他7 个比特，即0～6位，则实际上用于指定CMOS RAM 单元的索引号。这么说吧，NMI 中断应当始终是允许的，在访问RTC 时，我们直接关闭NMI，访问结束后，再打开NMI，而不管它以前到底是什么样子。

### 3.3 BCD
CMOS RAM 中保存的日期和时间，通常是以二进制编码的十进制数（Binary Coded Decimal，BCD）。比如十进制数25，其二进制形式是00011001。但是，如果采用BCD 编码的话，则一个字节的高4 位和低4 位分别独立地表示一个0 到9 之间的数字。因此，十进制数25 对应的BCD 编码是00100101。

介绍完了外部硬件中断的结构和原理，接下来我们来实现一个在屏幕上显示一个动态走动的时钟。这个示例需要使用上一节介绍的加载器程序加载执行。

## 4. 示例:动态时钟
### 4.1 初始化段
```
SECTION header vstart=0                     ;定义用户程序头部段 
    program_length  dd program_end          ;程序总长度[0x00]
    
    ;用户程序入口点
    code_entry      dw start                ;偏移地址[0x04]
                    dd section.code.start   ;段地址[0x06] 
    
    realloc_tbl_len dw (header_end-realloc_begin)/4
                                            ;段重定位表项个数[0x0a]
    
    realloc_begin:
    ;段重定位表           
    code_segment    dd section.code.start   ;[0x0c]
    data_segment    dd section.data.start   ;[0x14]
    stack_segment   dd section.stack.start  ;[0x1c]
    
header_end:                
;===============================================================================
SECTION code align=16 vstart=0           ;定义代码段（16字节对齐） 
new_int_0x70:
    ........

bcd_to_ascii:                            ;BCD码转ASCII
                                         ;输入：AL=bcd码
    ........
;-------------------------------------------------------------------------------
start:
      mov ax,[stack_segment]
      mov ss,ax
      mov sp,ss_pointer
      mov ax,[data_segment]
      mov ds,ax
      
      mov bx,init_msg                    ;显示初始信息 
      call put_string

      mov bx,inst_msg                    ;显示安装信息 
      call put_string
```

`mov ss,ax` 用于设置栈段寄存器，当处理器执行任何一条改变栈段寄存器SS 的指令时，它会在下一条指令执行完期间禁止中断。原因是栈段的更改需要两条指令，更改 ss，然后更改 sp，而如果在这之间发生了中断，将出现非常严重的问题。

因此，处理器在设计的时候就规定，当遇到修改段寄存器SS 的指令时，在这条指令和下一条指令执行完毕期间，禁止中断，以此来保护栈。换句话说，你应该在修改段寄存器SS 的指令之后，紧跟着一条修改栈指针SP 的指令。

### 4.2 设置中断向量表

```
      mov al,0x70
      mov bl,4
      mul bl                             ;计算0x70号中断在IVT中的偏移
      mov bx,ax                          

      cli                                ;防止改动期间发生新的0x70号中断

      push es
      mov ax,0x0000
      mov es,ax
      mov word [es:bx],new_int_0x70      ;偏移地址。
                                          
      mov word [es:bx+2],cs              ;段地址
      pop es
```

RTC 芯片的中断信号，通向中断控制器8259 从片的第1 个中断引脚IR0。在计算机启动期间，BIOS 会初始化中断控制器，将主片的中断号设为从0x08 开始，将从片的中断号设为从0x70开始。

新的中断处理过程是从标号 new_int_0x70 处开始的，new_int_0x70 位于代码段内。

### 4.3 设置 RTC 中断类型
RTC 到8259 的中断线只有一根，而RTC 可以产生多种中断，比如
1. 闹钟中断
2. 更新结束中断: 每当RTC 更新了CMOS RAM 中的日期和时间后，将发出此中断
3. 周期性中断。

RTC 的计时（更新周期）是独立的，产生中断信号只是它的一个赠品。所以，如果希望它能产生中断信号，需要额外设置。我们先来看更新结束中断的设置:

```
      mov al,0x0b                        ;RTC寄存器B
      or al,0x80                         ;阻断NMI 
      out 0x70,al
      mov al,0x12                        ;设置寄存器B，禁止周期性中断，开放更新结束后中断
      out 0x71,al                        ;BCD码，24小时制 
```

在访问RTC 期间，最好是阻断NMI，所以
1. 在 `out 0x70,al` 写入RTC寄存器B 前将 0x70 最高位设置为 1。
2. `mov al,0x12` 0x12 是写入 RTC寄存器B 的值，表示允许更新周期照常发生，禁止周期性中断，禁止闹钟功能，允许更新周期结束中断，使用24 小时制，日期和时间采用BCD 编码。

### 4.4 复位未决的中断状态
每次当中断实际发生时，可以在程序（中断处理过程）中读寄存器C 的内容来检查中断的原因。比如，每当更新周期结束中断发生时，RTC 就将它的第4 位置1。该寄存器还有一个特点，就是每次读取它后，所有内容自动清零。而且，如果不读取它的话（换句话说，相应的位没有清零），同样的中断将不再产生。为此我们需要读一下寄存器C 的内容，使之开始产生中断信号。

```
      mov al,0x0c
      out 0x70,al
      in al,0x71                         ;读RTC寄存器C，复位未决的中断状态
```

注意，在向索引端口0x70 写入的同时，也打开了NMI。

### 4.5 修改中断屏蔽寄存器IMR
RTC 芯片设置完毕后，再来打通它到8259 的最后一道屏障。正常情况下，8259 是不会允许RTC 中断的，所以，需要修改它内部的中断屏蔽寄存器IMR。

```
      in al,0xa1                         ;读8259从片的IMR寄存器 
      and al,0xfe                        ;清除bit 0(此位连接RTC)
      out 0xa1,al                        ;写回此寄存器 

      sti                                ;重新开放中断 

      mov bx,done_msg                    ;显示安装完成信息 
      call put_string

      mov bx,tips_msg                    ;显示提示信息
      call put_string
```

sti 指令将标志寄存器的IF 位置1，开放设备中断。从这个时候开始，中断随时都会发生，也随时会被处理。

### 4.6 显示时钟中心
```
      mov cx,0xb800
      mov ds,cx
      mov byte [12*160 + 33*2],'@'       ;屏幕第12行，35列
       
 .idle:
      hlt                                ;使CPU进入低功耗状态，直到用中断唤醒
      not byte [12*160 + 33*2+1]         ;反转显示属性 
      jmp .idle
```
`mov cx,0xb800` 使段寄存器DS 指向显示缓冲区，并在屏幕上的第12 行33 列显示一个字符“@”。

hlt 指令使处理器停止执行指令，并处于停机状态，这将降低处理器的功耗。处于停机状态的处理器可以被外部中断唤醒并恢复执行，而且会继续执行hlt 后面的指令。相对于jmp $指令，使用hlt 指令会大大降低处理器的占用率。

## 5.实时时钟中断的处理过程
主程序就是这样了，下面就来看看RTC 的更新周期结束中断处理。

### 5.1 保存现场
```
new_int_0x70:
      push ax
      push bx
      push cx
      push dx
      push es
```

### 5.2 判断更新周期是否结束
```
  .w0:                                    
      mov al,0x0a                        ;阻断NMI。当然，通常是不必要的
      or al,0x80                          
      out 0x70,al
      in al,0x71                         ;读寄存器A
      test al,0x80                       ;测试第7位UIP 
      jnz .w0                            ;以上代码对于更新周期结束中断来说 
                                         ;是不必要的 
```
读RTC 寄存器A，根据UIP 位的状态来决定是等待更新周期结束，还是继续往下执行。UIP 位为0 表示现在访问CMOS RAM 中的日期和时间是安全的。test 指令用于测试寄存器AL 的第7 位是否为1。

test 指令在功能上和and 指令是一样的，都是将两个操作数按位进行逻辑“与”，并根据结果设置相应的标志位。但是，test 指令执行后，运算结果被丢弃（不改变或破坏两个操作数的内容）。和and 指令一样，test 指令执行后，OF＝CF＝0；对ZF、SF 和PF 的影响视测试结果而定；对AF 的影响未定义。

正常情况下，访问CMOS RAM 中的日期和时间，必须等待RTC 更新周期结束，所以上面的判断过程是必需的，而这些代码也适用于正常的访问过程。但是，当前中断处理过程是针对更新周期结束中断的，而当此中断发生时，本身就说明对CMOS RAM 的访问是安全的，毕竟留给我们的时间是999 毫秒，这段时间非常充裕，这段时间能执行千万条指令。所以，在这种特定的情况下，上面的判断过程是不必要的。当然，加上倒也无所谓。

### 5.3 读取时分秒
分别访问CMOS RAM 的0、2、4 号单元，从中读取当前的秒、分、时数据，按顺序压栈等待后续操作。
```
      xor al,al
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(秒)
      push ax

      mov al,2
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(分)
      push ax

      mov al,4
      or al,0x80
      out 0x70,al
      in al,0x71                         ;读RTC当前时间(时)
      push ax
```

### 5.4 中断复位

```
      mov al,0x0c                        ;寄存器C的索引。且开放NMI 
      out 0x70,al
      in al,0x71                         ;读一下RTC的寄存器C，否则只发生一次中断
                                         ;此处不考虑闹钟和周期性中断的情况 
```

读一下RTC 的寄存器C，使得所有中断标志复位。这等于是告诉RTC，中断已经得到处理，可以继续下一次中断。否则的话，RTC 看到中断未被处理，将不再产生中断信号。RTC 产生中断的原因有多种，可以在程序中通过读寄存器C 来判断具体的原因。不过这里不需要，因为除了更新周期结束中断

### 5.5 在屏幕上显示时间
```     
      mov ax,0xb800
      mov es,ax

      pop ax
      call bcd_to_ascii
      mov bx,12*160 + 36*2               ;从屏幕上的12行36列开始显示

      mov [es:bx],ah
      mov [es:bx+2],al                   ;显示两位小时数字

      mov al,':'
      mov [es:bx+4],al                   ;显示分隔符':'
      not byte [es:bx+5]                 ;反转显示属性 

      pop ax
      call bcd_to_ascii
      mov [es:bx+6],ah
      mov [es:bx+8],al                   ;显示两位分钟数字

      mov al,':'
      mov [es:bx+10],al                  ;显示分隔符':'
      not byte [es:bx+11]                ;反转显示属性

      pop ax
      call bcd_to_ascii
      mov [es:bx+12],ah
      mov [es:bx+14],al                  ;显示两位秒数字
      ;......中断结束指令......... 
;-------------------------------------------------------------------------------
bcd_to_ascii:                            ;BCD码转ASCII
                                         ;输入：AL=bcd码
                                         ;输出：AX=ascii
      mov ah,al                          ;分拆成两个数字 
      and al,0x0f                        ;仅保留低4位 
      add al,0x30                        ;转换成ASCII 

      shr ah,4                           ;逻辑右移4位 
      and ah,0x0f                        
      add ah,0x30

      ret
```
bcd_to_ascii 来将用BCD 码表示的“小时”转换成ASCII(代码见下)。

### 5.6 中断结束
```
      mov al,0x20                        ;中断结束命令EOI 
      out 0xa0,al                        ;向从片发送 
      out 0x20,al                        ;向主片发送 

      pop es
      pop dx
      pop cx
      pop bx
      pop ax

      iret
```

在8259 芯片内部，有一个中断服务寄存器（Interrupt Service Register，ISR），这是一个8 位寄存器，每一位都对应着一个中断输入引脚。当中断处理过程开始时，8259 芯片会将相应的位置1，表明正在服务从该引脚来的中断。

一旦响应了中断，8259 中断控制器无法知道该中断什么时候才能处理结束。同时，如果不清除相应的位，下次从同一个引脚出现的中断将得不到处理。在这种情况下，需要程序在中断处理过程的结尾，显式地对8259 芯片编程来清除该标志，方法是向8259 芯片发送中断结束命令（End Of Interrupt，EOI）。

中断结束命令的代码是0x20。需要注意的是，如果外部中断是8259 主片处理的，那么，EOI 命令仅发送给主片即可，端口号是0x20；如果外部中断是由从片处理的，就像本章的例子，那么，EOI 命令既要发往从片（端口号0xa0），也要发往主片。

最后指令iret 回到中断之前的地方继续执行。iret 的意思是Interrupt Return。

## 6. 内部中断
和硬件中断不同，内部中断发生在处理器内部，是由执行的指令引起的。比如:
1. 当处理器检测到div 或者idiv 指令的除数为零时，或者除法的结果溢出时，将产生中断0（0 号中断），这就是除法错中断。
2. 当处理器遇到非法指令时，将产生中断6

内部中断不受标志寄存器 IF 位的影响，也不需要中断识别总线周期，它们的中断类型是固定的，可以立即转入相应的处理过程。

## 7. 软中断
软中断是由int 指令引起的中断处理。这类中断也不需要**中断识别总线周期**，中断号在指令中给出。int 指令的格式如下：
1. int3: 断点中断指令，机器指令码为CC，与调试时断点有关。
2. int imm8: 通常所说的int n，其操作码为0xCD，第2 字节的操作数给出了中断号
3. into: 
      - 溢出中断指令，机器码为0xCE，也是单字节指令
      - 当处理器执行这条指令时，如果标志寄存器的OF 位是1，那么，将产生4 号中断。否则，这条指令什么也不做。

## 8. BIOS 中断
中断是操作系统提供系统调用的主要方式，因为不像jmp 和call 指令，int 指令不需要知道目标程序的入口地址。每次操作系统加载完自己之后，以中断处理程序的形式提供硬盘读写功能，并把该例程的地址填写到中断向量表中。无论在什么时候，用户程序需要该功能时，直接发出一个软中断即可，不需要知道具体的地址。

中断处理程序在计算机加电之后，最先在 BIOS 程序执行期间建立起来的。换句话说，这些中断功能在加载和执行主引导扇区之前，就已经可以使用了。这些由 BIOS 建立的中断叫做BIOS 中断，又称BIOS 功能调用，主要是为了方便地使用最基本的硬件访问功能。

不同的硬件使用不同的中断号，通常，为了区分针对同一硬件的不同功能，使用寄存器AH 来指定具体的功能编号。比如:

```
mov ah 0x00
int 0x16
```

使用键盘服务时，中断号是0x16。当寄存器AH 的内容是0x00 时，执行int 0x16 后，中断服务例程会监视键盘动作。当它返回时，会在寄存器AL 中存放按键的ASCII 码。

### 8.1 BIOS 中断的建立
你可能觉得奇怪，BIOS 是怎么建立起这套功能调用中断的？它又是怎么知道如何访问硬件的？

答案是，BIOS 可能会为一些简单的外围设备提供初始化代码和功能调用代码，并填写中断向量表，但也有一些BIOS 中断是由外部设备接口自己建立的。首先，每个外部设备接口，包括各种板卡，如网卡、显卡、键盘接口电路、硬件控制器等，都有自己的只读存储器（Read Only Memory，ROM），类似于BIOS 芯片，这些ROM 中提供了它自己的功能调用例程，以及本设备的初始化代码。按照规范，前两个单元的内容是0x55 和0xAA，第三个单元是本ROM 中以512 字节为单位的代码长度；从第四个单元开始，就是实际的ROM 代码。

其次，我们知道，从内存物理地址A0000 开始，到FFFFF 结束，有相当一部分空间是留给外围设备的。如果设备存在，那么，它自带的ROM 会映射到分配给它的地址范围内。在计算机启动期间，BIOS 程序会以2KB 为单位搜索内存地址C0000～E0000 之间的区域。当它发现某个区域的头两个字节是0x55 和0xAA 时，那意味着该区域有ROM 代码存在，是有效的。接着，它对该区域做累加和检查，看结果是否和第三个单元相符。如果相符，就从第四个单元进入。这时，处理器执行的是硬件自带的程序指令，这些指令初始化外部设备的相关寄存器和工作状态，最后，填写相关的中断向量表，使它们指向自带的中断处理过程。

