---
title: 10. 程序的动态加载和执行
date: 2021-07-10
categories:
    - Go
tags:
	- x86 汇编
---

保护模式四
<!-- more -->

## 1. 内容概述
本节我们将实现一个微微内核，来介绍程序的动态加载和执行。这个微内核分为三个部分:
1. 主引导程序负责加载内核，并转交控制权
2. 内核负责加载用户程序，并提供各种例程给用户程序调用。提供给用户程序调用的例程也叫应用程序接口（Application Programming Interface，API）
3. 用户程序完成具体的功能实现

通过这个示例，我们将学习操作系统在保护模式下加载和重定位应用程序的一般原理，学习简单的内存动态分配，了解应用程序接口API 的简单原理。

## 2. 内核的结构、功能和加载
### 2.1 内核的结构
内核分为四个部分，分别是
1. 初始化代码，主引导程序也是初始化代码的组成部分
    - 用于从BIOS 那里接管处理器和计算机硬件的控制权，安装最基本的段描述符，初始化最初的执行环境
    - 然后，从硬盘上读取和加载内核的剩余部分，创建组成内核的各个内存段
2. 内核代码段
    - 用于分配内存，读取和加载用户程序，控制用户程序的执行
3. 内核数据段
    - 提供了一段可读写的内存空间，供内核自己使用
4. 公共例程段
    - 提供各种用途和功能的子过程以简化代码的编写。
    - 这些例程既可以用于内核，也供用户程序调用。
5. 内核文件还包括一个头部，记录了各个段的汇编位置，这些统计数据用于告诉初始化代码如何加载内核。

内核整体的代码结构如下:

```
16 内核头部数据，供加载时定位内核的各个部分

34 section sys_routine vstart=0  ; 公用例程


330 SECTION core_data vstart=0   ;系统核心的数据段

385 SECTION core_code vstart=0   ;内核代码段

SECTION core_trail               ;尾部用于计算内核长度
```

### 2.2 内核的加载
#### 内存布局
下面是所有程序的内存布局示意图:


![内存布局示意图](/images/assembly/memory_asign.png)

|开始|结束|长度|区域|
|:---|:---|:---|:---|
|0x00006C00|0x00007C00|4KB=2^12|内核栈|
|0x00007C00|0x00007E00|2*2^8|主引导程序|
|0x00007E00|0x0003FFFF|最大64KB|GDT|
|0x00040000|0x0009FFFF||内核程序|
|0x000A0000|0x000FFFFF||ROM BIOS，硬件专有的|
|0x00100000|||用户程序|

#### 主引导程序
主引导程序与我们之前编写的主引导程序基本类似，大体的结构如下:

```
         core_base_address equ 0x00040000   ;常数，内核加载的起始内存地址 
         core_start_sector equ 0x00000001   ;常数，内核的起始逻辑扇区号 
         
         mov ax,cs      
         mov ss,ax
         mov sp,0x7c00
      
         ;计算GDT所在的逻辑段地址
         mov eax,[cs:pgdt+0x7c00+0x02]      ;GDT的32位物理地址 
         xor edx,edx
         mov ebx,16
         div ebx                            ;分解成16位逻辑地址 

         mov ds,eax                         ;令DS指向该段以进行操作
         mov ebx,edx                        ;段内起始偏移地址 
         ;1. 设置全局描述符表
         ;跳过0#号描述符的槽位 
         ;创建1#描述符，这是一个数据段，对应0~4GB的线性地址空间
         .....

  flush:                                  
         mov eax,0x0008                     ;加载数据段(0..4GB)选择子
         mov ds,eax
      
         mov eax,0x0018                     ;加载堆栈段选择子 
         mov ss,eax
         xor esp,esp                        ;堆栈指针 <- 0 
         
         ;2.从磁盘读取内核程序到磁盘
         mov edi,core_base_address 
      
         mov eax,core_start_sector
         mov ebx,edi                        ;起始地址 
         call read_hard_disk_0              ;以下读取程序的起始部分（一个扇区） 
         ......
 setup:
         mov esi,[0x7c00+pgdt+0x02]         ;不可以在代码段内寻址pgdt，但可以
                                            ;通过4GB的段来访问
         ;3.建立公用例程段描述符
         mov eax,[edi+0x04]                 ;公用例程代码段起始汇编地址
         mov ebx,[edi+0x08]                 ;核心数据段汇编地址
         sub ebx,eax
         dec ebx                            ;公用例程段界限 
         add eax,edi                        ;公用例程段基地址
         mov ecx,0x00409800                 ;字节粒度的代码段描述符
         call make_gdt_descriptor
         .....
         lgdt [0x7c00+pgdt]                  

         jmp far [edi+0x10]
read_hard_disk_0:                        ;从硬盘读取一个逻辑扇区
         ......
make_gdt_descriptor:                     ;构造描述符
         ......
;-------------------------------------------------------------------------------
         pgdt             dw 0
                          dd 0x00007e00      ;GDT的物理地址
;-------------------------------------------------------------------------------                             
         times 510-($-$$) db 0
                          db 0x55,0xaa
```

第二步**从磁盘读取内核程序**和第三步**安装内核的段描述符**都与内核头部数据有关，内核头部数据如下:

```
         ;以下常量定义部分。内核的大部分内容都应当固定 
         core_code_seg_sel     equ  0x38    ;内核代码段选择子          00111_000 == 7
         core_data_seg_sel     equ  0x30    ;内核数据段选择子          00110_000 == 6
         sys_routine_seg_sel   equ  0x28    ;系统公共例程代码段的选择子 00101_000 == 5
         video_ram_seg_sel     equ  0x20    ;视频显示缓冲区的段选择子   00100_000 == 4
         core_stack_seg_sel    equ  0x18    ;内核堆栈段选择子          00011_000  == 3
                                            ;                         2 号描述符给了 mbr
         mem_0_4_gb_seg_sel    equ  0x08    ;整个0-4GB内存的段的选择子 00001_000  == 1

;-------------------------------------------------------------------------------
         ;以下是系统核心的头部，用于加载核心程序 
         core_length      dd core_end       ;核心程序总长度#00

         sys_routine_seg  dd section.sys_routine.start
                                            ;系统公用例程段位置#04

         core_data_seg    dd section.core_data.start
                                            ;核心数据段位置#08

         core_code_seg    dd section.core_code.start
                                            ;核心代码段位置#0c


         core_entry       dd start          ;核心代码段入口点#10
                          dw core_code_seg_sel

;===============================================================================
         [bits 32]
```

例程 make_gdt_descriptor 用于根据段的基本信息动态计算其对应的描述符
1. 需要三个参数，段的基地址用EAX 寄存器传入；段界限用EBX 寄存器传入，但只用其低20 位；段属性用ECX 寄存器传入
2. 各属性位在ECX 寄存器中的分布和它们在描述符高32 位中的时候一样，其他和段属性无关的位都清零。
3. 返回：EDX:EAX=完整的描述符

make_gdt_descriptor 的代码如下:

```
make_gdt_descriptor:                     ;构造描述符
                                         ;输入：EAX=线性基地址
                                         ;      EBX=段界限
                                         ;      ECX=属性（各属性位都在原始
                                         ;      位置，其它没用到的位置0） 
                                         ;返回：EDX:EAX=完整的描述符
         mov edx,eax                     
         shl eax,16                      ;ax 高16就是线性地址的低16位
         or ax,bx                        ;ax 全为 0, bx 保存到 ax 中，这样ax 保存的就是低 16 段界限，描述符前32位(EAX)构造完毕
      
         and edx,0xffff0000              ;清除基地址中无关的位
         rol edx,8                       
         bswap edx                       ;装配基址的31~24和23~16  (80486+)
      
         xor bx,bx                       ;获取段界限高 4
         or edx,ebx                      ;装配段界限的高4位
      
         or edx,ecx                      ;装配属性 
      
         ret
```

内核描述符安装完毕后，全局描述符表的结构如下图所示:

![内核加载完成后的GDT 布局](/images/assembly/cor_gdt.png)

最后 `jmp far [edi+0x10]` 跳转到内核头部 `core_entry` 定义的内核入口。至此控制权从主引导程序转移到内核。

## 3. 用户程序的加载和重定位
内核的主要任务就是加载和执行用户程序，我们将其定义成了一个例程 load_relocate_program，作用是加载和重定位用户程序。用户程序必须符合规定的格式，才能被内核识别和加载。我们自定义的用户程序格式如下:

```
;===============================================================================
SECTION header vstart=0

         program_length   dd program_end          ;程序总长度#0x00
         
         head_len         dd header_end           ;程序头部的长度#0x04

         stack_seg        dd 0                    ;用于接收堆栈段选择子#0x08
         stack_len        dd 1                    ;程序建议的堆栈大小#0x0c
                                                  ;以4KB为单位
                                                  
         prgentry         dd start                ;程序入口#0x10 
         code_seg         dd section.code.start   ;代码段位置#0x14
         code_len         dd code_end             ;代码段长度#0x18

         data_seg         dd section.data.start   ;数据段位置#0x1c
         data_len         dd data_end             ;数据段长度#0x20
             
;-------------------------------------------------------------------------------
         ;符号地址检索表
         salt_items       dd (header_end-salt)/256 ;#0x24
         
         salt:                                     ;#0x28
         PrintString      db  '@PrintString'
                     times 256-($-PrintString) db 0
                     
         TerminateProgram db  '@TerminateProgram'
                     times 256-($-TerminateProgram) db 0
                     
         ReadDiskData     db  '@ReadDiskData'
                     times 256-($-ReadDiskData) db 0
                 
header_end:
```

内核在加载用户程序时，会为用户程序分配内存，并将栈选择子、代码段选择子、数据选择子回填到用户程序中去:
1. 栈选择子 -> stack_seg
2. 代码段选择子 -> code_seg
3. 数据选择子 -> data_seg

### 3.1 load_relocate_program
load_relocate_program 加载用户程序分成了如下几个步骤:
1. 读取用户程序的头部第一个扇区，获取用户程序的大小，并计算用户程序占用的磁盘扇区数
2. 为用户程序分配内存，将用户程序读取到分配的内存处
3. 根据用户头部信息来创建段描述符
4. 重定位用户程序内的符号地址

#### 重定位用户程序内的符号地址
为了使用内核提供的例程，用户程序需要建立一个符号-地址对照表（SALT）。

为了对用户程序内的符号名进行匹配，内核也必须建立一张符号-地址对照表（SALT）。如下图所示:
1. 用户程序内的SALT 表，每个条目是256 字节，用于容纳符号名，不足256字节的，用零填充。
2. 内核中的SALT 表，每个条目则包括两部分，第一部分也是256 字节的符号名；第二部分有6 字节，用于容纳4 字节的偏移地址和2 字节的段选择子，因为符号名是用来描述例程的，这6 字节就是例程的入口地址。

![重定位用户程序内的符号地址](/images/assembly/relocation.png)

这样，当用户程序加载后，内核应该根据这些符号名来回填它们对应的入口地址，这称为符号地址的重定位。显然，重定位的过程就是字符串匹配和比较的过程。

#### 跳转至用户程序
用户程序加载完成后，通过 `jmp far [0x10]` 将控制权转交给用户程序。

```
         mov [esp_pointer],esp               ;临时保存堆栈指针
       
         mov ds,ax                           ;使段寄存器DS 指向用户程序头部
      
         jmp far [0x10]                      ;控制权交给用户程序（入口点）
                                             ;堆栈可能切换 
```